apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.getui.logful.sample"
        minSdkVersion 10
        targetSdkVersion 22
        versionCode 100
        versionName "0.2.1"
        manifestPlaceholders = [
                GETUI_APP_ID    : "NXsD3hcKyl7orMs78ma5K6",
                GETUI_APP_KEY   : "4Zh4PgmqaS61nLI7wBGz04",
                GETUI_APP_SECRET: "yecD1E5kMEAAdmOlBRHpq7",
                PACKAGE_NAME    : applicationId
        ]
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
    }
}

def getArtifactsDir() {
    //noinspection GroovyAssignabilityCheck
    def artifactsDir = new File(rootProject.projectDir, project.buildOutputDir)
    if (!artifactsDir.exists()) {
        artifactsDir.mkdirs()
    }
    return artifactsDir
}

def getApkFileName(variant) {
    def buildTime = buildTime()
    def buildTypeName = variant.buildType.name
    def versionName = variant.mergedFlavor.versionName
    def versionCode = variant.mergedFlavor.versionCode
    def type = "${buildTypeName}_${versionName}_${versionCode}"
    def markFile = new File("$project.buildDir/CI_BUILD_MARK")
    if (markFile.exists()) {
        markFile.text = buildTime
    }
    return "${project.name}_android_${type}_${buildTime}.apk"
}

def buildTime() {
    return new Date().format("yyyyMMdd_HHmmss", TimeZone.getTimeZone("Asia/Shanghai"))
}

def changeAndroidBuildConfig() {
    android.applicationVariants.all { variant ->
        def artifactsDir = getArtifactsDir()

        // 拷贝 mapping.txt 至目标目录
        if (variant.buildType.isMinifyEnabled()) {
            assemble << {
                copy {
                    from variant.mappingFile
                    into artifactsDir
                    rename 'mapping.txt', getMapFileName(variant)
                }
            }
        }

        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                output.outputFile = new File(artifactsDir, getApkFileName(variant))
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':logful')
    compile 'com.igexin:sdk:2.6.4.0'
    compile 'com.android.support:appcompat-v7:22.1.0'
}

task buildPrepare << {
    changeAndroidBuildConfig()
}

task assembleAppDebug(dependsOn: ['buildPrepare', 'assembleDebug'])
task assembleAppRelease(dependsOn: ['buildPrepare', 'assembleRelease'])

//noinspection GroovyAssignabilityCheck
assembleDebug.mustRunAfter buildPrepare
assembleRelease.mustRunAfter buildPrepare
